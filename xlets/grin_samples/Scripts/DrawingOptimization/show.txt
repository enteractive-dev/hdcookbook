#
# A show file for testing various features of GRIN.  Generally, these are
# grouped as related segments.  Each group of related segments and features
# is set off in the file with a big block comment, and each group uses a
# common naming prefix.
#


show

setting draw_targets {
    T:Default
    T:Bunny
    T:Picture
} ;

####################################################################
#
#                       DRAWING OPTIMIZATION
#
# These segments help to demonstrate drawing optimization.  We have
# three independent animations going on:  A pair of turtle troopers
# moving back and forth, a picture that fades in and out, and
# a gun bunny who moves around the screen.
#
# The same animation is presented in three different segments:
#
#       Unoptimized  -- no effot at optimizing drawing
#       Targets      -- drawing targets used to reduce draw area
#       Guarantee    -- Like targets, plus a guarantee_fill operation
#                       is used to cut down on the erase overhead of
#                       the turtles
#
# To see what's going on, bring this up in grinviewer or GrinViewerXlet,
# and step through the animation in "Watch drawing" mode.  You should
# see the differences in the erase areas and the drawing areas.
#
# Remember that optimizing the erase areas so that large areas aren't
# unnecessarily erased is important, because every pixel in the erase
# area has to be drawn to.  Optimizing the draw area is also important,
# but it's OK if a large empty area of the screen is in the draw area;
# that doesn't hurt so much, because no drawing operations are actually
# done there.
# 
####################################################################

segment S:Initialize
    setup {
        F:DrawOpt.Everything
    } setup_done {
        activate_segment S:DrawOpt.Opt.TargetAndGuarantee ;
    }
;

segment S:DrawOpt.Unoptimized
    active {
        F:DrawOpt.Troopers
        F:DrawOpt.Troopers.Coords
        F:DrawOpt.Bunny
        F:DrawOpt.Bunny.Coords
        F:DrawOpt.FadingPicture
    } setup {
        F:DrawOpt.Everything
    }
    on_entry {
        reset_feature   F:DrawOpt.Troopers.Coords ;
        reset_feature   F:DrawOpt.Bunny.Coords ;
                # We reset the trooper and bunny coords on this segment only.
                # This is done to test the "reset_feature" command.
    }
;

segment S:DrawOpt.Opt.Target
    active {
        F:DrawOpt.Troopers
        F:DrawOpt.Troopers.Coords
        F:DrawOpt.Bunny.Targeted
        F:DrawOpt.Bunny.Coords
        F:DrawOpt.FadingPicture.Targeted
    } setup {
        F:DrawOpt.Everything
    }
;

segment S:DrawOpt.Opt.TargetAndGuarantee
    active {
        F:DrawOpt.Troopers.Guaranteed
        F:DrawOpt.Troopers.Coords
        F:DrawOpt.Bunny.Targeted
        F:DrawOpt.Bunny.Coords
        F:DrawOpt.FadingPicture.Targeted
    } setup {
        F:DrawOpt.Everything
    }
;


feature set_target F:DrawOpt.Bunny.Targeted
        F:DrawOpt.Bunny T:Bunny ;

feature set_target F:DrawOpt.FadingPicture.Targeted
        F:DrawOpt.FadingPicture T:Picture ;


feature translation F:DrawOpt.Troopers.Coords {
        0        200 900                linear
        40      1700 900                linear
        80       200 900                linear
}  repeat 1 ;


feature translator F:DrawOpt.Troopers 
    F:DrawOpt.Troopers.Coords {
        F:DrawOpt.Trooper.1
        F:DrawOpt.Trooper.2
    } 
;

feature translator F:DrawOpt.Troopers.Guaranteed
    F:DrawOpt.Troopers.Coords {
        F:DrawOpt.Troopers.Guaranteed.Guarantee
    }
;

feature guarantee_fill F:DrawOpt.Troopers.Guaranteed.Guarantee
     F:DrawOpt.Troopers.Guaranteed.Group
        # two 63x110 images at 200, 900 and 300, 900
    ( 200 900 362 1009 )        # guarantee area is 163x110
    {
        ( 263 900 299 1009 )    # Need to fill the in-between rect with 
                                # transparent pixels
    }
;

feature group F:DrawOpt.Troopers.Guaranteed.Group {
        F:DrawOpt.Trooper.1
        F:DrawOpt.Trooper.2
} ;

feature fixed_image F:DrawOpt.Trooper.1 200 900 "images/turtle_trooper.png" ;
feature fixed_image F:DrawOpt.Trooper.2 300 900 "images/turtle_trooper.png" ;

feature translation F:DrawOpt.Bunny.Coords {
          0      100  100       linear
         30      100  700       linear
         70     1720  650       linear
        100      100  100       linear
} repeat 1 ;

feature translator F:DrawOpt.Bunny
    F:DrawOpt.Bunny.Coords {
        F:DrawOpt.Bunny.Im
    }
;


feature fixed_image F:DrawOpt.Bunny.Im 100 100 "images/bunny_00.png" ;
        # 175x200 pixels


feature fade F:DrawOpt.FadingPicture F:DrawOpt.Picture src_over { 
        0       0       linear
        30      255     linear
        80      255     linear
        100     0       linear
} repeat 1 ;

feature guarantee_fill F:DrawOpt.Picture.Guaranteed
     F:DrawOpt.Picture          # it's 400x537
    ( 1500 500 1899 1036 )
    { }                         #  No fill areas needed
;

feature fixed_image F:DrawOpt.Picture 1500 500 "images/bunny_with_gun.png" ;


feature group F:DrawOpt.Everything {
        F:DrawOpt.Troopers
        F:DrawOpt.Troopers.Coords
        F:DrawOpt.Bunny.Coords
        F:DrawOpt.Bunny
        F:DrawOpt.FadingPicture
        F:DrawOpt.FadingPicture.Targeted
        F:DrawOpt.Troopers.Guaranteed
        F:DrawOpt.Bunny.Targeted
} ;


####################################################################
#  End of show file
####################################################################

end_show
